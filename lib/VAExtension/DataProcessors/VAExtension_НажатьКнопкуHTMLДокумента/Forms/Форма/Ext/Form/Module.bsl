#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьКодКлиент(Команда)
	СсылкаНайдена = Ложь;
	
	Окна = ПолучитьОкна();
	Результат = Новый Массив;
	
	ОкноНайдено = Ложь;
	ОкноНачальнойСтраницы = Неопределено;
	
	Для Каждого ТекОкно Из Окна Цикл
		
		
		Если ТекОкно.Заголовок = ЗначениеЗаголовокОкна Тогда
			Если ТекОкно.Содержимое.Количество() > 0 Тогда
				ПолеHTMLДокумента = ТекОкно.Содержимое[0].Элементы[ЗначениеИмяЭлементаФормы];
				НажатьНаКнопку(ПолеHTMLДокумента);
				ОкноНайдено = Истина;
				Прервать;
			КонецЕсли;	 
		КонецЕсли;	 
		
		Если ЗначениеЗаголовокОкна = "Начальная страница" И ТекОкно.НачальнаяСтраница И ТекОкно.Заголовок = "" Тогда
			// Иногда начальная страница не содержит заголовка, но отображается она с заголовком "Начальная страница"
			// Тоесть ванесса ищет окно с заголовком "Начальная страница", но такого окна в списке "ПолучитьОкна()" нет
			// проверим на эти условия и запомним окно
			ОкноНачальнойСтраницы = ТекОкно;
		КонецЕсли;	

	КонецЦикла;
	
	Если НЕ ОкноНайдено И ОкноНачальнойСтраницы <> Неопределено тогда
		Если ОкноНачальнойСтраницы.Содержимое.Количество() > 0 Тогда
			// Мы не нашли окно, но зато нашли окно начальной страницы без заголовка
			ПолеHTMLДокумента = ОкноНачальнойСтраницы.Содержимое[0].Элементы[ЗначениеИмяЭлементаФормы];
			НажатьНаКнопку(ПолеHTMLДокумента);
		КонецЕсли;	
	
	КонецЕсли;	
	
	ВыражениеВычислено = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура НажатьНаКнопку(ПолеHTMLДокумента)
	
	Если ТипКнопки = 0 Тогда
		//Нажатие кнопки по номеру
		ПолеHTMLДокумента.Документ.querySelectorAll("[type=button]")[Число(ИмяКнопки)].click();
		СсылкаНайдена = Истина;
	Иначе
		//Если ТипКнопки = 1  Тогда
		//Нажатие кнопки по имени
		ШаблонПоиска = "[type=button][name=""%1""]";
		Buttons = ПолеHTMLДокумента.Документ.querySelectorAll(СтрШаблон(ШаблонПоиска, ИмяКнопки));

		Если Buttons.length > 0 Тогда
			Кнопка = Buttons[0];
		    Кнопка.click();
			СсылкаНайдена = Истина;
		КонецЕсли;
		
	 КонецЕсли;	 
	 
КонецПроцедуры 

#КонецОбласти