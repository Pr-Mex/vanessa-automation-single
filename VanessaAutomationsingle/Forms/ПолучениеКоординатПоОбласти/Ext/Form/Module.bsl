
#Область ОписаниеПеременных

&НаКлиенте
Перем Ванесса, ТочкаX1, ТочкаY1;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ДескрипторОсновногоОкнаТекущегоКлиентаТестирования = Параметры.ДескрипторОсновногоОкнаТекущегоКлиентаТестирования;
	ИспользоватьКомпонентуVanessaExt = Параметры.ИспользоватьКомпонентуVanessaExt;
	ДескрипторОсновногоОкнаVA = Параметры.ДескрипторОсновногоОкнаVA;
КонецПроцедуры

&НаКлиенте
Процедура ВнешнееСобытие(Источник, Событие, Данные)
	
	//Сообщить("Внешнее событие: " + Источник + ",  " + Событие + ",  " + Данные);
	
	Если Источник = "AddIn.WindowsControl" И Событие = "MOUSEHOOK" Тогда
		
		ДанныеСобытия = Ванесса.ПрочитатьСтрокуJSON(Данные);
		//Если Ванесса.ПрочитатьСтрокуJSON(Данные).window = клиент тестирования тогда
		Если ДанныеСобытия.event = "LBUTTONDOWN" Тогда
			
			ТочкаX1 = ДанныеСобытия.x;
			ТочкаY1 = ДанныеСобытия.y;
			
		ИначеЕсли ДанныеСобытия.event = "LBUTTONUP" Тогда
			
			ТочкаX2 = ДанныеСобытия.x;
			ТочкаY2 = ДанныеСобытия.y;
			
			ОбластьXОтрицательная = ((ТочкаX2 - ТочкаX1) < 0);
			ОбластьYОтрицательная = ((ТочкаY2 - ТочкаY1) < 0);
			
			// Линия справа снизу вверх влево.
			Если ОбластьXОтрицательная И ОбластьYОтрицательная Тогда
				ТочкаX = ТочкаX2;
				ТочкаY = ТочкаY2;
				ОбластьWidth = ТочкаX1 - ТочкаX2;
				ОбластьHeight = ТочкаY1 - ТочкаY2;
			// Линия сверху справа вниз влево.
			ИначеЕсли ОбластьXОтрицательная Тогда
				ТочкаX = ТочкаX2;
				ТочкаY = ТочкаY1;
				ОбластьWidth = ТочкаX1 - ТочкаX2;
				ОбластьHeight = ТочкаY2 - ТочкаY1;
			// Линия снизу слева вверх вправо.
			ИначеЕсли ОбластьYОтрицательная Тогда
				ТочкаX = ТочкаX1;
				ТочкаY = ТочкаY2;
				ОбластьWidth = ТочкаX2 - ТочкаX1;
				ОбластьHeight = ТочкаY1 - ТочкаY2;
			// Линия сверху слева вниз вправо.
			Иначе
				ТочкаX = ТочкаX1;
				ТочкаY = ТочкаY1;
				ОбластьWidth = ТочкаX2 - ТочкаX1;
				ОбластьHeight = ТочкаY2 - ТочкаY1;
			КонецЕсли;
			
			Ванесса.ВнешняяКомпонентаДляСкриншотов.МониторингСобытийМыши = Ложь;
			Если Не АктивноОкноТестКлиента Тогда
				ФигураПрямоугольник();
			КонецЕсли;
			
			ДвоичныеДанные = Ванесса.ВнешняяКомпонентаДляСкриншотов.ПолучитьСнимокОбласти(
				ТочкаX, ТочкаY, ОбластьWidth, ОбластьHeight);
			
			Принскрин = ПоместитьВоВременноеХранилище(ДвоичныеДанные, УникальныйИдентификатор);
			
			// Вернемся обратно в инструмент получения снимка.
			Если ИспользоватьКомпонентуVanessaExt
				И ДескрипторОсновногоОкнаVA <> 0 Тогда
				
				Оповещение = Новый ОписаниеОповещения("ОбработкаПослеНачатьВызовАктивироватьОкно", Ванесса);
				Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовАктивироватьОкно(Оповещение, ДескрипторОсновногоОкнаVA);
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ИнициализацияФормы(ВладелецФормы) Экспорт
	Ванесса = ВладелецФормы;
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура МониторингСобытийНачать(Команда)
	
	АктивноОкноТестКлиента = Ложь;
	Если ИспользоватьКомпонентуVanessaExt
		И ДескрипторОсновногоОкнаТекущегоКлиентаТестирования <> 0 Тогда
		
		АктивноОкноТестКлиента = Истина;
		Оповещение = Новый ОписаниеОповещения("ОбработкаПослеНачатьВызовАктивироватьОкно", Ванесса);
		Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовАктивироватьОкно(Оповещение, ДескрипторОсновногоОкнаТекущегоКлиентаТестирования);
		
	КонецЕсли;
	
	Ванесса.ВнешняяКомпонентаДляСкриншотов.МониторингСобытийМыши = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ФигураПрямоугольник()
	
	НастройкиРисования = ПолучитьНастройкиРисования();
	Ванесса.ВнешняяКомпонентаДляСкриншотов.НачатьВызовНарисоватьПрямоугольник(
		Новый ОписаниеОповещения, НастройкиРисования,
		ТочкаX, ТочкаY, ОбластьWidth, ОбластьHeight);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьНастройкиРисования(ИспользуемКнопки = Ложь)
	
	ВремСтруктура = Новый Структура;
	ВремСтруктура.Вставить("color", 255);
	ВремСтруктура.Вставить("transparency", 254);
	ВремСтруктура.Вставить("duration", 3000);
	ВремСтруктура.Вставить("thickness", 2);
	ВремСтруктура.Вставить("frameDelay", 20);
	ВремСтруктура.Вставить("fontName", "Calibri");
	ВремСтруктура.Вставить("fontSize", 09);
	//Если ИспользуемКнопки Тогда
	//	ВремСтруктура.Вставить("buttons", МассивКнопок);
	//	ВремСтруктура.Вставить("margin", 30);
	//КонецЕсли;
	
	Возврат Ванесса.ЗаписатьОбъектJSON(ВремСтруктура);
	
	//"color": 3289800,
	//"transparency": 126,
	//"duration": 3000,
	//"thickness": 4,
	//"frameDelay": 20,
	//"fontName": "Calibri",
	//"fontSize": 12
	
КонецФункции

#КонецОбласти

#Область ИнициализацияПеременных

#Если Клиент Тогда
	ТочкаX1 = 0;
	ТочкаY1 = 0;
#КонецЕсли

#КонецОбласти









