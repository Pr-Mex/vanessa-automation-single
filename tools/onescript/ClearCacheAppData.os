#Использовать v8runner
#Использовать logos
#Использовать cmdline

Перем Лог;

Процедура УдалитьКеш(Путь)
	Массив = Новый Массив;
	
	Файлы = НайтиФайлы(Путь,"*",Истина);
	Для Каждого Файл Из Файлы Цикл
		Если Файл.ЭтоКаталог() Тогда
			Если СтрЧислоВхождений(Файл.Имя,"-") = 4 Тогда
				Сообщить(Файл.ПолноеИмя);
				Массив.Добавить(Файл.ПолноеИмя);
			КонецЕсли;	 
		КонецЕсли;	
	КонецЦикла;	
	
	Для Каждого ИмяКаталога Из Массив Цикл
		Файлы = НайтиФайлы(ИмяКаталога,"*",Истина);
		Для Каждого Файл Из Файлы Цикл
			Если НЕ Файл.ЭтоКаталог() Тогда
				Попытка
					УдалитьФайлы(Файл.ПолноеИмя);
				Исключение
					Сообщить(ОписаниеОшибки());
				КонецПопытки;
			КонецЕсли;	 
		КонецЦикла;	
	КонецЦикла;	 
	
	Для Каждого ИмяКаталога Из Массив Цикл
		Попытка
			УдалитьФайлы(ИмяКаталога);
		Исключение
		КонецПопытки;
	КонецЦикла;	 
	
	Для Каждого ИмяКаталога Из Массив Цикл
		Попытка
			Файл = Новый Файл(ИмяКаталога); 
			Если Файл.Существует() Тогда
				УдалитьФайлы(ИмяКаталога);
			КонецЕсли;	 
		Исключение
			Сообщить(ОписаниеОшибки());
		КонецПопытки;
	КонецЦикла;	 
	
КонецПроцедуры 

Процедура ПолучитьДанные(ПутьAppdataRoaming = Неопределено, ПутьAppdataLocal = Неопределено)
	Если ПутьAppdataRoaming = Неопределено Тогда

		ВременныйBatФайл = ПолучитьИмяВременногоФайла("bat");
		ИмяФайлаЛога = ПолучитьИмяВременногоФайла("txt");
		ЗТ = Новый ЗаписьТекста(ВременныйBatФайл,"UTF-8",,Истина); 
		ЗТ.ЗаписатьСтроку("echo %Appdata% > " + ИмяФайлаЛога); 
		ЗТ.Закрыть();
		
		
		СтрокаКоманды = ВременныйBatФайл;
		ЗапуститьПриложение(СтрокаКоманды,,Истина);
		
		Текст = Новый ЧтениеТекста;
		Текст.Открыть(ИмяФайлаЛога,"UTF-8");
		ПутьAppdataRoaming = СокрЛП(Текст.Прочитать());
		Текст.Закрыть();
		
		Сообщить("ПутьAppdata="+ПутьAppdataRoaming);
		
	КонецЕсли;
	Путь1 = ПутьAppdataRoaming + "\1C";
	Сообщить("Путь1="+Путь1);

	Если ПутьAppdataLocal = Неопределено Тогда

		ФайлAppdataRoaming = Новый Файл(ПутьAppdataRoaming);
		ПутьAppdata = ФайлAppdataRoaming.Путь;
		Сообщить("ПутьAppdata="+ПутьAppdata);
		
		Путь2 = ПутьAppdata + "Local\1C";
	Иначе
		Путь2 = ПутьAppdataLocal + "\1C";
	КонецЕсли;
	Сообщить("Путь2="+Путь2);
	
	УдалитьКеш(Путь1);
	УдалитьКеш(Путь2);
	
	
КонецПроцедуры 

Функция РазобратьАргументыКоманднойСтроки()

	Если АргументыКоманднойСтроки.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;

	Парсер = Новый ПарсерАргументовКоманднойСтроки();
	Парсер.ДобавитьИменованныйПараметр("-appdata");
	Парсер.ДобавитьИменованныйПараметр("-localappdata");
	Парсер.ДобавитьИменованныйПараметр("-userprofile");
	Параметры = Парсер.Разобрать(АргументыКоманднойСтроки);

	Возврат Параметры;

КонецФункции

Процедура ПоказатьИнформациюОПараметрахКоманднойСтроки()

	Сообщить("Очистка кэша 1С.");
	Сообщить("Использование: ");
	Сообщить("	<без параметров> 
			|		будет очищен кэш текущего пользователя (под которым запускается скрипт)
			|	-appdata <путь к папке Roaming, например C:\Users\UserName\AppData\Roaming (можно получить как echo %AppData%)>
			|		будет очищен кэш в подпапке 1C и в подпапке ..\Local\1C
			|	-appdata <путь к папке Roaming, например C:\Users\UserName\AppData\Roaming (можно получить как echo %AppData%)> -localappdata <путь к папке Local, например C:\Users\UserName\AppData\Local (можно получить как echo %LocalAppData%)>
			|		будет очищен кэш в подпапках 1C этих папок
			|	-userprofile <путь к папке текущего пользователя, например C:\Users\UserName (можно получить как echo %UserProfile%)>
			|		будет очищен кэш в подпапках AppData\Roaming\1C и AppData\Local\1C");

КонецПроцедуры

Процедура ЗавершитьСкрипт(КодВозврата)
	Лог.Закрыть();

	ЗавершитьРаботу(КодВозврата);
КонецПроцедуры

Лог = Логирование.ПолучитьЛог("vb.compile.log");
Лог.УстановитьУровень(УровниЛога.Отладка);

Попытка
	ПутьAppdataRoaming = Неопределено;
	ПутьAppdataLocal = Неопределено;
	ПутьUserProfile = Неопределено;

	Параметры = РазобратьАргументыКоманднойСтроки();
Исключение
	ПоказатьИнформациюОПараметрахКоманднойСтроки();
	Лог.Ошибка(ОписаниеОшибки());
	ЗавершитьСкрипт(-1);
КонецПопытки;
Если ТипЗнч(Параметры) = Тип("Соответствие") Тогда
	Сообщить("Переданы параметры");
	Если Параметры["-appdata"] <> Неопределено Тогда
		
		Сообщить("ПутьAppdataRoaming=" + Параметры["-appdata"]);			
		Сообщить("ПутьAppdataLocal=" + Параметры["-localappdata"]);			
		ПолучитьДанные(Параметры["-appdata"], Параметры["-localappdata"]);
	ИначеЕсли Параметры["-userprofile"] <> Неопределено Тогда

		Сообщить("ПутьUserProfile=" + Параметры["-userprofile"]);			
		ПолучитьДанные(Параметры["-userprofile"] + "\AppData\Roaming", Параметры["-userprofile"] + "\AppData\Local");
	Иначе
		ПоказатьИнформациюОПараметрахКоманднойСтроки();
		Лог.Ошибка("Указаны некорректные аргументы командной строки");
		ЗавершитьСкрипт(-1);
	КонецЕсли;
Иначе
	Сообщить("Параметры не переданы");
	Сообщить("Тип переменной Параметры = " + ТипЗнч(Параметры));
	ПолучитьДанные();
КонецЕсли;

//Сообщить("////////////////////");
//Сообщить("Обработка завершена.");
Sleep(1000);



