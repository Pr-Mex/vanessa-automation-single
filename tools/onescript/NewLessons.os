#Использовать v8runner
#Использовать logos

Перем Лог;

Процедура ВыполнитьОбработку(ПутьКСпискуУроковССылкамиВидеоИДругимиСсылками, ТекущийКаталогУроков, ПредыдущийКаталогУроков)
	
	ТекущиеЗначенияУроков = ЗначенияУроковИзКаталога(ТекущийКаталогУроков);
	ПредыдущиеЗначенияУроков = ЗначенияУроковИзКаталога(ПредыдущийКаталогУроков);
	
	НовыеУроки = Новый Массив;
	ИзмененныеУроки = Новый Массив;
	
	Для Каждого ТекУрок Из ТекущиеЗначенияУроков Цикл
		ИмяУрока = ТекУрок.Ключ;
		
		Если НЕ ПредыдущиеЗначенияУроков.Свойство(ИмяУрока) Тогда
			НовыеУроки.Добавить(ИмяУрока);
			//Сообщить("Новый урок: " + ИмяУрока);
			Продолжить;
		КонецЕсли;	
		
		Текст1 = СтрЗаменить(СокрЛП(НРег(ТекУрок.Значение)), "^", "");
		Текст2 = СтрЗаменить(СокрЛП(НРег(ПредыдущиеЗначенияУроков[ИмяУрока])), "^", "");
		
		Если Текст1 <> Текст2 Тогда
			ИзмененныеУроки.Добавить(ИмяУрока);
			//Сообщить("Измененный урок: " + ИмяУрока);
			Продолжить;
		КонецЕсли;	
		
	КонецЦикла;	
	
	Текст = Новый ЧтениеТекста;
	Текст.Открыть(ПутьКСпискуУроковССылкамиВидеоИДругимиСсылками, "UTF-8");
	ТекстФайла = Текст.Прочитать();
	Текст.Закрыть();        
	
	ДанныеСсылок = Новый Соответствие;
	
	МассивСтрок = СтрРазделить(ТекстФайла, Символы.ПС);
	Результат = Новый Массив;
	Для Сч = 0 По МассивСтрок.Количество()-1 Цикл
		Стр = МассивСтрок[Сч];
		
		Если Найти(Стр, "[(Видео)]") = 0 Тогда
			Результат.Добавить(Стр);
			Продолжить;
		КонецЕсли;	   
		
		ЭтоНовыйУрок = Ложь;
		СтрокуНадоОставить = СтрокуНадоОставить(Стр, НовыеУроки, ИзмененныеУроки, ЭтоНовыйУрок);
		Если НЕ СтрокуНадоОставить Тогда
			Продолжить;
		КонецЕсли;	
		
		Если ЭтоНовыйУрок Тогда
			Стр = СтрЗаменить(Стр, "- ", "- (New) ");
			Стр = СтрЗаменить(Стр, "1. ", "1. (New) ");
			//ДлинаОтступа = СтрДлина(Стр) - СтрДлина(СокрЛ(Стр));
			//Отступ = Лев(Стр, ДлинаОтступа);
			//Стр = Отступ + "New! " + СокрЛ(Стр);
		КонецЕсли;	
		Результат.Добавить(Стр);
		
	КонецЦикла;
	
	ВставитьЗаголовок(Результат);
	УбратьСлужебныеСтроки(Результат);
	УбратьПустыеГруппы(Результат);
	
	ТекстФайла = СтрСоединить(Результат, Символы.ПС);
	
	Файл = Новый Файл(ПутьКСпискуУроковССылкамиВидеоИДругимиСсылками);
	НовоеИмяФайла = Файл.Путь + "New.MD";
	Сообщить("НовоеИмяФайла: " + НовоеИмяФайла);
	ЗТ = Новый ЗаписьТекста(НовоеИмяФайла,"UTF-8",, Ложь); 
	ЗТ.Записать(ТекстФайла); 
	ЗТ.Закрыть();
	
КонецПроцедуры

Процедура УбратьСлужебныеСтроки(Результат)
	
	МассивИД = Новый Массив;
	Для Сч = 0 По Результат.Количество()-2 Цикл
		Стр = Результат[Сч];
		
		Если Найти(Стр, "что нужно для работы интерактивных уроков") > 0 Тогда
			МассивИД.Добавить(Сч);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Сч = 0 По МассивИД.Количество()-1 Цикл
		Ид = МассивИД[МассивИД.Количество()-1-Сч];
		Результат.Удалить(Ид);
	КонецЦикла;
	
КонецПроцедуры

Процедура УбратьПустыеГруппы(Результат)
	
	Итерация = 0;
	
	Пока Истина Цикл
		
		Итерация = Итерация + 1;
		Сообщить("Итерация:" + Итерация);
		
		МассивИД = Новый Массив;
		Для Сч = 0 По Результат.Количество()-2 Цикл
			Стр = Результат[Сч];
			
			Если Найти(Стр, "[(Видео)]") > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Отступ1 = СтрДлина(Стр) - СтрДлина(СокрЛ(Стр));
			
			СледующаяСтрока = Результат[Сч + 1];
			
			Если НЕ ЗначениеЗаполнено(СледующаяСтрока) Тогда
				Сообщить("УдалениеПустаяСтрока=" + Стр);
				МассивИД.Добавить(Сч);
				Продолжить;
			КонецЕсли;	
			
			Если Найти(СледующаяСтрока, "[(Видео)]") > 0 Тогда
				Продолжить;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(Стр) И ЗначениеЗаполнено(СледующаяСтрока) Тогда
				Продолжить;
			КонецЕсли;	
			
			Отступ2 = СтрДлина(СледующаяСтрока) - СтрДлина(СокрЛ(СледующаяСтрока));
			Если Отступ1 < Отступ2 Тогда
				Продолжить;
			КонецЕсли;
			
			Сообщить("Удаление=" + Стр);
			//Сообщить("Отступ1=" + Отступ1);
			//Сообщить("Отступ2=" + Отступ2);
			МассивИД.Добавить(Сч);
			
		КонецЦикла;
		
		Если МассивИД.Количество() = 0 Тогда
			Прервать;
		КонецЕсли;	
		
		Для Сч = 0 По МассивИД.Количество()-1 Цикл
			Ид = МассивИД[МассивИД.Количество()-1-Сч];
			Результат.Удалить(Ид);
		КонецЦикла;	
	КонецЦикла;	
	
КонецПроцедуры

Процедура ВставитьЗаголовок(Результат)
	
	Для Сч = 0 По Результат.Количество()-1 Цикл
		Стр = Результат[Сч];
		Если Найти(Стр, "## Список уроков") > 0 Тогда
			Результат[Сч] = "## Список уроков добавленых или измененых с предыдущего релиза";
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

Функция СтрокуНадоОставить(Стр, НовыеУроки, ИзмененныеУроки, ЭтоНовыйУрок)
	
	НРег_Стр = НРег(Стр);
	
	Для Каждого ИмяУрока Из НовыеУроки Цикл
		Если Найти(НРег_Стр, НРег(ИмяУрока)) > 0 Тогда
			ЭтоНовыйУрок = Истина;
			Возврат Истина;
		КонецЕсли;	
	КонецЦикла;	
	
	Для Каждого ИмяУрока Из ИзмененныеУроки Цикл
		Если Найти(НРег_Стр, НРег(ИмяУрока)) > 0 Тогда
			Возврат Истина;
		КонецЕсли;	
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции	

Функция ЗначенияУроковИзКаталога(Путь)
	
	Результат = Новый Структура;
	
	Файлы = НайтиФайлы(Путь, "*.feature", Истина);
	Для Каждого Файл Из Файлы Цикл
		
		Если Найти(НРег(Файл.ПолноеИмя), "\примеры\") > 0 Тогда
			Продолжить;
		КонецЕсли;	
		
		Текст = Новый ЧтениеТекста;
		Текст.Открыть(Файл.ПолноеИмя, "UTF-8");
		ТекстФайла = Текст.Прочитать();
		Текст.Закрыть();
		
		НовыйМассивСтрок = Новый Массив;
		МассивСтрок = СтрРазделить(ТекстФайла, Символы.ПС);
		Для Сч = 0 По МассивСтрок.Количество()-1 Цикл
			Стр = СокрЛП(МассивСтрок[Сч]);
			
			Если НЕ ЗначениеЗаполнено(Стр) Тогда
				Продолжить;
			КонецЕсли;
			
			Если Лев(Стр, 2) = "//" Тогда
				Продолжить;
			ИначеЕсли Лев(Стр, 1) = "#" Тогда
				Продолжить;
			КонецЕсли;	
			
			НовыйМассивСтрок.Добавить(Стр);
		КонецЦикла;	
		
		ТекстФайла = СтрСоединить(НовыйМассивСтрок, Символы.ПС);
		
		Результат.Вставить(Файл.ИмяБезРасширения, ТекстФайла);
		
	КонецЦикла;	
	
	Возврат Результат;
	
КонецФункции	

Процедура ПолучитьЗначенияИзСтроки(Знач Стр, Значения)
	Поз = Найти(Стр, "[");
	Стр = Сред(Стр, Поз);
	
	Пока Найти(Стр, "]") > 0 Цикл
		Поз = Найти(Стр, "]");
		ЛевЧасть = Лев(Стр, Поз);
		ПраваяЧасть = Сред(Стр, Поз + 1);
		
		ЛевЧасть = СтрЗаменить(ЛевЧасть, "[", "");
		ЛевЧасть = СтрЗаменить(ЛевЧасть, "]", "");
		ЛевЧасть = СтрЗаменить(ЛевЧасть, "(", "");
		ЛевЧасть = СтрЗаменить(ЛевЧасть, ")", "");
		ИмяЗначения = СокрЛП(ЛевЧасть);
		
		
		Поз = Найти(ПраваяЧасть, ")");
		Ссылка = Лев(ПраваяЧасть, Поз);
		Ссылка = СтрЗаменить(Ссылка, "(", "");
		Ссылка = СтрЗаменить(Ссылка, ")", "");
		
		Стр = Сред(ПраваяЧасть, Поз + 1);
		
		Значения.Вставить(ИмяЗначения, Ссылка);
	КонецЦикла;	
	
КонецПроцедуры

Лог = Логирование.ПолучитьЛог("vb.compile.log");
Лог.УстановитьУровень(УровниЛога.Отладка);


Если АргументыКоманднойСтроки.Количество() = 0 Тогда
	Лог.Ошибка("Не переданы параметры!");
ИначеЕсли АргументыКоманднойСтроки.Количество() <> 3 Тогда
	Лог.Ошибка("Скрипт принимает три параметра!");
Иначе
	ВыполнитьОбработку(АргументыКоманднойСтроки[0], АргументыКоманднойСтроки[1], АргументыКоманднойСтроки[2]);
КонецЕсли;

